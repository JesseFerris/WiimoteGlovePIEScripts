/*
    Wiimote-CounterStrike2-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Open and run this script with GlovePIE.exe by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)

    Description: Emulates Mouse and Keyboard from Wiimote input.

    Game Settings: Default Bindings.

     ___________
    | @   _     |       Up: Drop Weapon
    |   _| |_   |     Left: Reload
    |  |_   _|  |    Right: Inspect Weapon
    |    |_|    |     Down: Use
    |           |
    |    (A)    |        A: Secondary Fire
    |           |        B: Fire
    |           |
    |  -  &  +  |     Plus: Select Next Weapon, Re-buy                           (Pressed 1x, Helddown)
    |           |     Home: Buy-Menu, Scoreboard                                 (Pressed 1x, Helddown)
    |     #     |    Minus: Select Previous Weapon, Auto-Buy                     (Pressed 1x, Helddown)
    |           |
    |    (1)    |      One: Primary Weapon, Cycle Grenades, Mele Weapons         (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |    (2)    |      Two: Secondary Weapon, HE Grenades, Explosive Traps       (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |  o o o o  |      Joy: Move, Walk
    |    Wii    |        C: Jump
    |___________|        Z: Crouch

*/

//  Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

//  Box Size
var.BoxWidth = 20
var.BoxHeight = 15

//  Sensitivity Adjustments
var.Sensitivity = 45
var.AutoTurnSpeed = 250

//  Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 250ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    If Helddown(Wiimote.PointerVisible, 500ms)

        Mouse.DirectInputX += var.ModifyerX
        Mouse.DirectInputY += var.ModifyerY

    EndIf

Endif

//  Button Mapping
Key.G                =    Wiimote.Up
Key.R                =    Wiimote.Left
Key.F                =    Wiimote.Right
Key.E                =    Wiimote.Down

Mouse.RightButton    =    Wiimote.A
Mouse.LeftButton     =    Wiimote.B

Mouse.WheelDown      =    Keepdown(Wiimote.Plus, 250ms) and not Wiimote.Plus
Key.F4               =    Helddown(Wiimote.Plus, 250ms)
Key.B                =    Keepdown(Wiimote.Home, 150ms) and not Wiimote.Home
Key.Tab              =    Helddown(Wiimote.Home, 250ms)
Key.F3               =    Helddown(Wiimote.Minus, 250ms)
Mouse.WheelUp        =    Keepdown(Wiimote.Minus, 250ms) and not Wiimote.Minus

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms)
Key.4                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms)
Key.3                =    Helddown(Wiimote.One, 150ms)

Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms)
Key.6                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms)
Key.5                =    Helddown(Wiimote.Two, 150ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Shift            =    ((abs(Wiimote.Nunchuk.JoyX) < 0.45 and > 0.15) or (abs(Wiimote.Nunchuk.JoyY) < 0.45 and > 0.15)) but not ((abs(Wiimote.Nunchuk.JoyX) > 0.65) or (abs(Wiimote.Nunchuk.JoyY) > 0.65))

Key.Space            =    Wiimote.Nunchuk.CButton
Key.Ctrl             =    Wiimote.Nunchuk.ZButton
