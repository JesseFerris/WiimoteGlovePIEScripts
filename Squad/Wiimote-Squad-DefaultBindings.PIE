/*
    Wiimote-Squad-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Run this script with GlovePIE.exe created by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)
    Description: Emulates Mouse and Keyboard from Wiimote controller.
    Game Settings: Default Bindings.
*/

// Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

// Box Size
var.BoxWidth = 20
var.BoxHeight = 15

// Sensitivity Adjustments
var.Sensitivity = 10
var.AutoTurnSpeed = 35

// Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 750ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    Mouse.DirectInputX += var.ModifyerX
    Mouse.DirectInputY += var.ModifyerY

Else
    var.XMod = 0
    var.YMod = 0
Endif

// Button Mapping
If (Wiimote.Down but not Helddown(Wiimote.Down,10ms) and Wiimote.Nunchuk.CButton) then var.Counter += 1
If (Helddown(Wiimote.Down, 250ms)) or (Helddown(not Wiimote.Down, 500ms) and var.Counter > 0) then var.Counter = 0

Key.C                =    Helddown(Wiimote.Up, 250ms)
Key.R                =    Keepdown(Wiimote.Down, 250ms) and Helddown(not Wiimote.Down, 100ms) and not Wiimote.Nunchuk.CButton
Mouse.MiddleButton   =    Keepdown(not Wiimote.Down, 250ms) and Helddown(Wiimote.Down, 25ms) and not Wiimote.Nunchuk.CButton
Key.F                =    Helddown(Wiimote.Down, 150ms) and not Wiimote.Nunchuk.CButton
Key.F1               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 1)
Key.F2               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 2)
Key.F3               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 3)
Key.F4               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 4)
Key.F5               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 5)
Key.F6               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 6)
Key.F7               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 7)
Key.F8               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 8)
Key.F9               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 9)
Key.F1               =    Helddown(not Wiimote.Down, 250ms) and (var.Counter = 10)
Key.Q                =    Wiimote.Left
Key.E                =    Wiimote.Right

Key.Shift            =    Wiimote.A and Helddown(abs(Wiimote.Nunchuk.JoyX) < 0.15, 15ms) and Helddown(abs(Wiimote.Nunchuk.JoyY) < 0.15, 15ms)
Mouse.RightButton    =    (Keepdown(Wiimote.A, 150ms) but not Wiimote.A) or Helddown(Wiimote.A and Wiimote.B, 500ms)
Mouse.LeftButton     =    Wiimote.B

Mouse.WheelUp        =    Helddown(Wiimote.Plus, 250ms) or (Keepdown(Wiimote.Plus, 150ms) and not Wiimote.Plus)
Key.Left             =    Helddown(Wiimote.Plus, 250ms)
Key.M                =    Keepdown(Wiimote.Home, 250ms) and Helddown(not Wiimote.Home, 100ms)
Key.CapsLock         =    Keepdown(not Wiimote.Home, 250ms) and Helddown(Wiimote.Home, 25ms)
Key.X                =    Helddown(Wiimote.Plus, 200ms) or Helddown(Wiimote.Minus, 200ms)
Key.T                =    Helddown(Wiimote.Home, 150ms)
Key.Right            =    Helddown(Wiimote.Minus, 250ms)
Mouse.WheelDown      =    Helddown(Wiimote.Minus, 250ms) or (Keepdown(Wiimote.Minus, 150ms) and not Wiimote.Minus)

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms) and Helddown(not Wiimote.Two, 250ms)
Key.3                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms) and Helddown(not Wiimote.Two, 250ms)
Key.V                =    Helddown(Wiimote.One, 150ms) and Helddown(not Wiimote.Two, 250ms)
Key.G                =    Helddown(Wiimote.One and Wiimote.Two, 250ms)
Key.5                =    Keepdown(Wiimote.One and Wiimote.Two, 250ms) and Helddown(not (Wiimote.One and Wiimote.Two), 100ms)
Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms) and Helddown(not Wiimote.One, 250ms)
Key.4                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms) and Helddown(not Wiimote.One, 250ms)
Key.B                =    Helddown(Wiimote.Two, 150ms) and Helddown(not Wiimote.One, 250ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15

Key.Shift            =    (Wiimote.Nunchuk.JoyY < -0.65 and Helddown(not Wiimote.A, 500ms))
Key.Ctrl             =    Keepdown(Wiimote.Nunchuk.ZButton, 250ms) and Helddown(not Wiimote.Nunchuk.ZButton, 100ms)
Key.Z                =    Keepdown(not Wiimote.Nunchuk.ZButton, 250ms) and Helddown(Wiimote.Nunchuk.ZButton, 25ms)
Key.Space            =    Helddown(Wiimote.Nunchuk.ZButton, 250ms)
