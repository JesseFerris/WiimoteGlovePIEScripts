/*
    Wiimote-HalfLife-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Run this script with GlovePIE.exe created by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)
    Description: Emulates Mouse and Keyboard from Wiimote controller input.
    In-Game Settings: Default Bindings.
     ___________
    | @   _     |           Up: Use Items
    |   _| |_   |         Left: Last Weapon Used
    |  |_   _|  |        Right: Flashlight
    |    |_|    |         Down: Reload Weapon
    |           |    
    |    (A)    |            A: Secondary Attack
    |           |            B: Primary Attack
    |           |
    |  -  &  +  |         Plus: Next Weapon
    |           |         Home: Pause
    |     #     |        Minus: Previous Weapon
    |           |
    |    (1)    |          One: Weapon Category 2, 4    (Pressed 1x, Heldown)
    |           |    One & Two: Weapon Category 1
    |    (2)    |          Two: Weapon Category 3, 5    (Pressed 1x, Heldown)
    |           |
    |  o o o o  |          Joy: Move, Walk
    |    Wii    |            C: Jump
    |___________|            Z: Duck

*/

// Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

// Box Size
var.BoxWidth = 20
var.BoxHeight = 15

// Sensitivity Adjustments
var.Sensitivity = 20
var.AutoTurnSpeed = 40

// Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 750ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    Mouse.DirectInputX += var.ModifyerX
    Mouse.DirectInputY += var.ModifyerY

Else
    var.XMod = 0
    var.YMod = 0
Endif

// Button Mapping
Key.E                =    Wiimote.Up
Key.Q                =    Wiimote.Left
Key.F                =    Wiimote.Right
Key.R                =    Wiimote.Down

Mouse.RightButton    =    Wiimote.A
Mouse.LeftButton     =    Wiimote.B

Mouse.WheelDown      =    Wiimote.Plus
Key.Esc              =    Wiimote.Home
Mouse.WheelUp        =    Wiimote.Minus

Key.2                =    Keepdown(Wiimote.One, 250ms) and not Wiimote.One and Helddown (not Wiimote.Two, 250ms)
Key.4                =    Helddown(Wiimote.One, 150ms) and Helddown (not Wiimote.Two, 250ms)

Key.1                =    Wiimote.One and Wiimote.Two

Key.3                =    Keepdown(Wiimote.Two, 250ms) and not Wiimote.Two and Helddown (not Wiimote.One, 250ms)
Key.5                =    Helddown(Wiimote.Two, 150ms) and Helddown (not Wiimote.One, 250ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Shift            =    ((abs(Wiimote.Nunchuk.JoyX) < 0.45 and > 0.15) or (abs(Wiimote.Nunchuk.JoyY) < 0.45 and > 0.15)) but not ((abs(Wiimote.Nunchuk.JoyX) > 0.65) or (abs(Wiimote.Nunchuk.JoyY) > 0.65))

Key.Space            =    Wiimote.Nunchuk.CButton
Key.Ctrl             =    Wiimote.Nunchuk.ZButton
