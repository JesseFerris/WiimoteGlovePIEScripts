/*
    Wiimote-InsurgancySandstorm-HoldADS.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Run this script with GlovePIE.exe created by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)
    Description: Emulates Mouse and Keyboard from Wiimote controller input.
    In-Game Settings: Bind Mouse-Right Button to Aim Down Sights (Hold).
    Optional: For the best experience bind Alt to Walk (Hold) in-game, then in this script un-comment (remove the // in front of) the Key.Alt line under the Button Mapping section below.
     ___________
    | @   _     |           Up: Toggle Underbarrel Upgrade, Toggle Accessory, Melee Attack/Cycle Optics    (Pressed 1x, Pressed 2x, Helddown)
    |   _| |_   |         Left: Cycle Fire Mode, Lean Left                                                 (Pressed 1x, Helddown)
    |  |_   _|  |        Right: Toggle Siderail Upgrade, Lean Right                                        (Pressed 1x, Helddown)
    |    |_|    |         Down: Reload/Quick Reload/Inspect Ammo                                           (Pressed 1x/2x/held in game)
    |           |
    |    (A)    |            A: Aim Down Sights (Toggle), Focus (Hold)                                     (Pressed 1x, Helddown)
    |           |            B: Fire
    |           |
    |  -  &  +  |         Plus: Select Next Weapon
    |           |         Home: Keyboard Escape, Scoreboard/Tactical Map                                   (Pressed 1x, Helddown)
    |     #     |        Minus: Select Previous Weapon
    |           |
    |    (1)    |          One: Select Primary, Select Knife, Select Binoculars                            (Pressed 1x, Pressed 2x, Helddown)
    |           |    One & Two: Voice Chat Team Radio                                                      (Helddown)
    |    (2)    |          Two: Select Secondary, Select Explosive, Fire Support Menu                      (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |  o o o o  |          Joy: Move, Walk, Run
    |    Wii    |            C: Jump
    |___________|            Z: Prone, Crouch                                                              (Pressed 1x, Helddown)

*/

//  Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

//  Box Size
var.BoxWidth = 20
var.BoxHeight = 15

//  Sensitivity Adjustments
var.Sensitivity = 15
var.AutoTurnSpeed = 30

//  Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 750ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    Mouse.DirectInputX += var.ModifyerX
    Mouse.DirectInputY += var.ModifyerY

Else
    var.XMod = 0
    var.YMod = 0
Endif

//  Button Mapping
Key.C                =    Keepdown(Wiimote.Up, 250ms) and Helddown(not Wiimote.Up, 100ms)
Key.B                =    Keepdown(not Wiimote.Up, 250ms) and Helddown(Wiimote.Up, 25ms)
Mouse.MiddleButton   =    Helddown(Wiimote.Up, 250ms)
Key.X                =    Keepdown(Wiimote.Left, 150ms) and not Wiimote.Left
Key.Q                =    Helddown(Wiimote.Left, 150ms)
Key.H                =    Keepdown(Wiimote.Right, 150ms) and not Wiimote.Right
Key.E                =    Helddown(Wiimote.Right, 150ms)
Key.F                =    Helddown(Wiimote.Down, 150ms)
Key.R                =    Wiimote.Down

Key.Shift            =    Helddown(Wiimote.A, 150ms) and Helddown(abs(Wiimote.Nunchuk.JoyX) < 0.15, 350ms) and Helddown(abs(Wiimote.Nunchuk.JoyY) < 0.15, 350ms)
Mouse.RightButton    =    Wiimote.A
Mouse.LeftButton     =    Wiimote.B

Mouse.WheelDown      =    Wiimote.Plus
Key.M                =    Helddown(Wiimote.Plus, 250ms)
Key.Esc              =    Keepdown(Wiimote.Home, 150ms) and not Wiimote.Home
Key.Tab              =    Helddown(Wiimote.Home, 250ms)
Key.N                =    Helddown(Wiimote.Minus, 250ms)
Mouse.WheelUp        =    Wiimote.Minus

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms)
Key.5                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms)
Key.4                =    Helddown(Wiimote.One, 150ms) and Helddown(not Wiimote.Two, 250ms)

Key.V                =    Helddown(Wiimote.One and Wiimote.Two, 250ms)

Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms)
Key.3                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms)
Key.J                =    Helddown(Wiimote.Two, 150ms) and Helddown(not Wiimote.One, 250ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Shift            =    Wiimote.Nunchuk.JoyY < -0.65 and Helddown(not Wiimote.A, 500ms) and Helddown(not Wiimote.Home, 250ms)
//Key.Alt              =    ((abs(Wiimote.Nunchuk.JoyX) < 0.45 and > 0.15) or (abs(Wiimote.Nunchuk.JoyY) < 0.45 and > 0.15)) but not ((abs(Wiimote.Nunchuk.JoyX) > 0.65) or (abs(Wiimote.Nunchuk.JoyY) > 0.65))

Key.Space            =    Wiimote.Nunchuk.CButton
Key.Ctrl             =    Helddown(Wiimote.Nunchuk.ZButton, 150ms)
Key.Z                =    Keepdown(Wiimote.Nunchuk.ZButton, 250ms) and not Wiimote.Nunchuk.Z
