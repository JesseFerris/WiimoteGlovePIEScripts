/*
    Wiimote-Squad44-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Open and run this script with GlovePIE.exe by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)

    Description: Emulates Mouse and Keyboard from Wiimote controller input.

    Game Settings: Default Bindings.

     ___________
    | @   _     |           Up: Melee Attack, Bipod/Attach Detach Bayonet, Engine Ignition    (Pressed 1x, Pressed 2x, Helddown)
    |   _| |_   |         Left: Lean Left
    |  |_   _|  |        Right: Lean Right
    |    |_|    |         Down: Change Fire Mode, Reload                                      (Pressed 1x, Helddown)
    |           |
    |    (A)    |            A: Aim Sight, Focus, Interact                                    (Pressed 1x, Helddown, Pressed With Nunchuk C)
    |           |            B: Fire
    |           |
    |  -  &  +  |         Plus: Previous Item                                                 (Pressed 1x, Helddown)
    |           |         Home: Map, Map Zoom, Leader Radial Menu                             (Pressed 1x, Helddown, Pressed With Nunchuck C)
    |     #     |        Minus: Next Item                                                     (Pressed 1x, Helddown)
    |           |
    |    (1)    |          One: Select To Weapon 1, 3, Local Voice                            (Pressed 1x, Pressed 2x, Helddown)
    |           |    One & Two: Bandage, Command Radio
    |    (2)    |          Two: Select To Weapon 2, 4, Section Radio                          (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |  o o o o  |          Joy: Move, Sprint
    |    Wii    |            C: Jump, Sight Ranging                                           (Pressed 1x, Helddown)
    |___________|            Z: Crouch, Prone                                                 (Pressed 1x, Helddown)

*/

//  Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

//  Box Size
var.BoxWidth = 20
var.BoxHeight = 15

//  Sensitivity Adjustments
var.Sensitivity = 10
var.AutoTurnSpeed = 35

//  Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 250ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    If Helddown(Wiimote.PointerVisible, 500ms)

        Mouse.DirectInputX += var.ModifyerX
        Mouse.DirectInputY += var.ModifyerY

    EndIf

Endif

//  Button Mapping
Key.CapsLock         =    Keepdown(Wiimote.Up, 250ms) and Helddown(not Wiimote.Up, 100ms)
Key.C                =    Keepdown(not Wiimote.Up, 250ms) and Helddown(Wiimote.Up, 25ms)
Key.I                =    Helddown(Wiimote.Up, 250ms)
Key.Q                =    Wiimote.Left
Key.E                =    Wiimote.Right
Key.R                =    Helddown(Wiimote.Down, 150ms)
Mouse.MiddleButton   =    Keepdown(Wiimote.Down, 250ms) and not Wiimote.Down

Key.Shift            =    Wiimote.A and Helddown(abs(Wiimote.Nunchuk.JoyX) < 0.15, 15ms) and Helddown(abs(Wiimote.Nunchuk.JoyY) < 0.15, 15ms)
Key.F                =    Helddown(Wiimote.A and Wiimote.Nunchuk.CButton, 150ms)
Mouse.RightButton    =    Keepdown(Wiimote.A, 150ms) but not Wiimote.A
Mouse.LeftButton     =    Wiimote.B

If (Wiimote.A but not Helddown(Wiimote.A,10ms)) and Wiimote.Nunchuk.CButton then var.Counter += 1
If (Helddown(Wiimote.A, 250ms)) or (Helddown(not Wiimote.A, 500ms) and var.Counter > 0) then var.Counter = 0

Key.F1               =    (var.Counter = 1)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 1 time.
Key.F2               =    (var.Counter = 2)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 2 times.
Key.F3               =    (var.Counter = 3)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 3 times.
Key.F4               =    (var.Counter = 4)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 4 times.
Key.F5               =    (var.Counter = 5)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 5 times.
Key.F6               =    (var.Counter = 6)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 6 times.
Key.F7               =    (var.Counter = 7)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 7 times.
Key.F8               =    (var.Counter = 8)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 8 times.
Key.F9               =    (var.Counter = 9)  and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 9 times.
Key.F10              =    (var.Counter = 10) and Helddown(not Wiimote.A, 250ms) // Nunchuk C Held and Wiimote A pressed 10 times.

Mouse.WheelUp        =    Helddown(Wiimote.Plus, 250ms) or (Keepdown(Wiimote.Plus, 150ms) and not Wiimote.Plus)
Key.T                =    Helddown(Wiimote.Home, 150ms) and Wiimote.Nunchuk.CButton
Key.N                =    Helddown(Wiimote.Home, 150ms) and not Wiimote.Nunchuk.CButton
Key.M                =    Keepdown(Wiimote.Home, 250ms) and Helddown(not Wiimote.Home, 100ms)
Mouse.WheelDown      =    Helddown(Wiimote.Minus, 250ms) or (Keepdown(Wiimote.Minus, 150ms) and not Wiimote.Minus)

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms) and Helddown(not Wiimote.Two, 250ms)
Key.3                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms) and Helddown(not Wiimote.Two, 250ms)
Key.V                =    Helddown(Wiimote.One, 150ms) and Helddown(not Wiimote.Two, 250ms)

Key.G                =    Helddown(Wiimote.One and Wiimote.Two, 250ms)
Key.5                =    Keepdown(Wiimote.One and Wiimote.Two, 250ms) and Helddown(not (Wiimote.One and Wiimote.Two), 100ms)

Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms) and Helddown(not Wiimote.One, 250ms)
Key.4                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms) and Helddown(not Wiimote.One, 250ms)
Key.B                =    Helddown(Wiimote.Two, 150ms) and Helddown(not Wiimote.One, 250ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Shift            =    Wiimote.Nunchuk.JoyY < -0.65 and not Wiimote.A

Key.X                =    Helddown(Wiimote.Nunchuk.CButton, 250ms)
Key.Space            =    Keepdown(Wiimote.Nunchuk.CButton, 250ms) and not Wiimote.Nunchuk.CButton
Key.Ctrl             =    Keepdown(Wiimote.Nunchuk.ZButton, 250ms) and Helddown(not Wiimote.Nunchuk.ZButton, 80ms)
Key.Z                =    Helddown(Wiimote.Nunchuk.ZButton, 150ms)
