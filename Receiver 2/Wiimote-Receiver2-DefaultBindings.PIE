/*
    Wiimote-Receiver2-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Open and run this script with GlovePIE.exe by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)

    Description: Emulates Mouse and Keyboard from Wiimote controller input.

    Game Settings: Default Bindings.

     ___________
    | @   _     |       Up: Interact/Pick Up
    |   _| |_   |     Left: Swing Out Cylinder/Eject Drop Magazine
    |  |_   _|  |    Right: Slide Lock, Close Cylinder                                 (Pressed 1x, Helddown)
    |    |_|    |     Down: Insert Bullets/Magazine, Use Extractor Rod, Cock Hammer
    |           |
    |    (A)    |        A: Aim (Toggle), Aim (Hold)
    |           |        B: Fire
    |           |
    |  -  &  +  |     Plus: Rotate Cylinder Clockwise
    |           |     Home: Holster/Equip Gun
    |     #     |    Minus: Rotate Cylinder Counter-Clockwise
    |           |
    |    (1)    |      One: Keyboard 1, 3, 5                                           (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |    (2)    |      Two: Keyboard 2, 4, Menu                                        (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |  o o o o  |      Joy: Move, Run
    |    Wii    |        C: Jump
    |___________|        Z: Crouch

*/

//  Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

//  Box Size
var.BoxWidth = 20
var.BoxHeight = 15

//  Sensitivity Adjustments
var.Sensitivity = 75
var.AutoTurnSpeed = 250

//  Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 250ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    If Helddown(Wiimote.PointerVisible, 500ms)

        Mouse.DirectInputX += var.ModifyerX
        Mouse.DirectInputY += var.ModifyerY

    EndIf

Endif

//  Button Mapping
Key.G                =    Wiimote.Up
Key.E                =    Wiimote.Left
Key.T                =    Keepdown(Wiimote.Right, 250ms) and not Wiimote.Right
Key.R                =    Helddown(Wiimote.Right, 150ms)
Key.Z                =    Keepdown(Wiimote.Down, 250ms) and Helddown(not Wiimote.Down, 100ms)
Key.V                =    Keepdown(not Wiimote.Down, 250ms) and Helddown(Wiimote.Down, 25ms)
Key.F                =    Helddown(Wiimote.Down, 150ms)

Key.Q                =    Keepdown(Wiimote.A, 250ms) and not Wiimote.A
Mouse.RightButton    =    Helddown(Wiimote.A, 100ms)
Mouse.LeftButton     =    Wiimote.B

Mouse.WheelUp        =    Wiimote.Plus
Key.Console          =    Wiimote.Home
Mouse.WheelDown      =    Wiimote.Minus

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms) and Helddown(not Wiimote.Two, 250ms)
Key.3                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms) and Helddown(not Wiimote.Two, 250ms)
Key.5                =    Helddown(Wiimote.One, 150ms) and Helddown(not Wiimote.Two, 250ms)

Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms) and Helddown(not Wiimote.One, 250ms)
Key.4                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms) and Helddown(not Wiimote.One, 250ms)
Key.Escape           =    Helddown(Wiimote.One and Wiimote.Two, 150ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Shift            =    Wiimote.Nunchuk.JoyY < -0.65

Key.Space            =    Wiimote.Nunchuk.CButton
Key.C                =    Wiimote.Nunchuk.ZButton
