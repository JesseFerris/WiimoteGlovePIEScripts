/*
    Wiimote-Quake-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Run this script with GlovePIE.exe created by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)
    Description: Emulates Mouse and Keyboard from Wiimote controller input.
    In-Game Settings: Default Bindings.
     ___________
    | @   _     |       Up: Axe, Thunderbolt                     (Pressed 1x, Helddown)
    |   _| |_   |     Left: Nailgun, Super Nailgun               (Pressed 1x, Helddown)
    |  |_   _|  |    Right: Grenade Launcher, Rocket Launcher    (Pressed 1x, Helddown)
    |    |_|    |     Down: Shotgun, Super Shotgun               (Pressed 1x, Helddown)
    |           |
    |    (A)    |        A: Attack
    |           |        B: Attack
    |           |
    |  -  &  +  |     Plus: Next Weapon
    |           |     Home: Weapon Wheel
    |     #     |    Minus: Previous Weapon
    |           |
    |    (1)    |      One: Axe                                  (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |    (2)    |      Two: Shotgun                              (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |  o o o o  |      Joy: Move, Walk
    |    Wii    |        C: Jump
    |___________|        Z: Swim Down

*/

//  Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

//  Box Size
var.BoxWidth = 20
var.BoxHeight = 15

//  Sensitivity Adjustments
var.Sensitivity = 30
var.AutoTurnSpeed = 80

//  Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 750ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    Mouse.DirectInputX += var.ModifyerX
    Mouse.DirectInputY += var.ModifyerY

Else
    var.XMod = 0
    var.YMod = 0
Endif

//  Button Mapping
Key.1                =    Keepdown(Wiimote.Up, 250ms) and not Wiimote.Up
Key.8                =    Helddown(Wiimote.Up, 150ms)
Key.4                =    Keepdown(Wiimote.Left, 250ms) and not Wiimote.Left
Key.5                =    Helddown(Wiimote.Left, 150ms)
Key.6                =    Keepdown(Wiimote.Right, 250ms) and not Wiimote.Right
Key.7                =    Helddown(Wiimote.Right, 150ms)
Key.2                =    Keepdown(Wiimote.Down, 250ms) and not Wiimote.Down
Key.3                =    Helddown(Wiimote.Down, 150ms)

Mouse.LeftButton     =    Wiimote.A
Mouse.LeftButton     =    Wiimote.B

Mouse.WheelUp        =    Wiimote.Plus
Key.Q                =    Wiimote.Home
Mouse.WheelDown      =    Wiimote.Minus

Key.1                =    Wiimote.One

Key.2                =    Wiimote.Two

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Shift            =    ((abs(Wiimote.Nunchuk.JoyX) < 0.45 and > 0.15) or (abs(Wiimote.Nunchuk.JoyY) < 0.45 and > 0.15)) but not ((abs(Wiimote.Nunchuk.JoyX) > 0.65) or (abs(Wiimote.Nunchuk.JoyY) > 0.65))

Key.Space            =    Wiimote.Nunchuk.CButton
Key.Z                =    Wiimote.Nunchuk.ZButton
