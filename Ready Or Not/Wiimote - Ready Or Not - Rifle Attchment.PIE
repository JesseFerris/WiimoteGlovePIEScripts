/*
    GlovePIE Script - Wiimote - Ready Or Not.
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
	How To Use: Connect Wiimote via Bluetooth. Run this script with GlovePIE.exe created by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)
	Description: Emulates Mouse and Keyboard from Wiimote input.
	Game Settings: Default Bindings.
*/

// Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

// Imaginary Box Size
var.BoxWidth = 20
var.BoxHeight = 15

// Sensitivity Adjustments
var.Sensitivity = 30
var.AutoTurnSpeed = 175

// Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 750ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    Mouse.DirectInputX += var.ModifyerX
    Mouse.DirectInputY += var.ModifyerY

Else
    var.XMod = 0
    var.YMod = 0
Endif

// Button Mapping
If Keepdown(Wiimote.A, 250ms) and Helddown(not Wiimote.A, 100ms) and Helddown(!var.ADS, 250ms) then var.ADS = True
If Keepdown(Wiimote.A, 250ms) and Helddown(not Wiimote.A, 100ms) and Helddown(var.ADS, 250ms) then var.ADS = False

If Keepdown(Wiimote.Down, 250ms) and Helddown(not Wiimote.Down, 100ms) and Helddown(!var.Crouch, 250ms) then var.Crouch = True
If Keepdown(Wiimote.Down, 250ms) and Helddown(not Wiimote.Down, 100ms) and Helddown(var.Crouch, 250ms) then var.Crouch = False

If Wiimote.Left and Helddown(!var.LeanLeft, 250ms) then var.LeanLeft = True
If (Wiimote.Left or Wiimote.Right) and Helddown(var.LeanLeft, 250ms) then var.LeanLeft = False

If Wiimote.Right and Helddown(!var.LeanRight, 250ms) then var.LeanRight = True
If (Wiimote.Right or Wiimote.Left) and Helddown(var.LeanRight, 250ms) then var.LeanRight = False

Key.Space            =    Keepdown(Wiimote.Up, 250ms) and Helddown(not Wiimote.Up, 100ms)
Key.P                =    Keepdown(not Wiimote.Up, 250ms) and Helddown(Wiimote.Up, 25ms)
Key.O                =    Helddown(Wiimote.Up, 150ms)
Key.Ctrl             =    var.Crouch
Key.Alt              =    Helddown(Wiimote.Down, 250ms)
Key.Q                =    var.LeanLeft
Key.E                =    var.LeanRight

Mouse.RightButton    =    var.ADS
Key.F                =    Keepdown(Wiimote.B, 250ms) and Helddown(not Wiimote.B, 100ms)
Key.F                =    Helddown(Wiimote.B, 500ms)

Mouse.WheelDown      =    Wiimote.Plus
Mouse.MiddleButton   =    Helddown(Wiimote.Home, 250ms)
Mouse.WheelUp        =    Wiimote.Minus

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms) and Helddown(not Wiimote.Two, 250ms)
Key.3                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms) and Helddown(not Wiimote.Two, 250ms)
Key.5                =    Helddown(Wiimote.One, 150ms) and Helddown(not Wiimote.Two, 250ms)
Key.V                =    Helddown(Wiimote.One and Wiimote.Two, 250ms)
Key.L                =    Keepdown(Wiimote.One and Wiimote.Two, 250ms) and not (Wiimote.One and Wiimote.Two)
Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms) and Helddown(not Wiimote.One, 250ms)
Key.4                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms) and Helddown(not Wiimote.One, 250ms)
Key.6                =    Helddown(Wiimote.Two, 150ms) and Helddown(not Wiimote.One, 250ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15

Key.Shift            =    ((abs(Wiimote.Nunchuk.JoyX) < 0.45 and > 0.15) or (abs(Wiimote.Nunchuk.JoyY) < 0.45 and > 0.15)) but not ((abs(Wiimote.Nunchuk.JoyX) > 0.65) or (abs(Wiimote.Nunchuk.JoyY) > 0.65))
Key.R                =    Wiimote.Nunchuk.CButton and not Wiimote.B
Key.X                =    Wiimote.Nunchuk.CButton and Wiimote.B
Mouse.LeftButton     =    Wiimote.Nunchuk.ZButton

If (Wiimote.Home but not Helddown(Wiimote.Home,10ms)) then var.Counter += 1
If (Helddown(Wiimote.Home, 250ms)) or (Helddown(not Wiimote.Home, 500ms) and var.Counter > 0) then var.Counter = 0

Key.1 = (var.Counter = 1)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 1 time.
Key.2 = (var.Counter = 2)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 2 times.
Key.3 = (var.Counter = 3)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 3 times.
Key.4 = (var.Counter = 4)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 4 times.
Key.5 = (var.Counter = 5)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 5 times.
Key.6 = (var.Counter = 6)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 6 times.
Key.7 = (var.Counter = 7)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 7 times.
Key.8 = (var.Counter = 8)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 8 times.
Key.9 = (var.Counter = 9)  and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 9 times.
Key.0 = (var.Counter = 10) and Helddown(not Wiimote.Home, 250ms) // Wii remote HOME pressed 10 times.
