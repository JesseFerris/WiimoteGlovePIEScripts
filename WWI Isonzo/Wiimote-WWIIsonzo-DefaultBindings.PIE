/*
    Wiimote-WWIIsonzo-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Run this script with GlovePIE.exe created by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)
    Description: Emulates Mouse and Keyboard from Wiimote controller input.
    In-Game Settings: Default Bindings.
     ___________
    | @   _     |           Up: Sight Mode, Command                              (Pressed 1x, Helddown)
    |   _| |_   |         Left: Reload
    |  |_   _|  |        Right: Ready Up
    |    |_|    |         Down: Use
    |           |
    |    (A)    |            A: Aim Down Sights, Breath                          (Pressed 1x, Helddown)
    |           |            B: Primary Fire
    |           |
    |  -  &  +  |         Plus: Previous Weapon
    |           |         Home: Map Zoom, Overview Map                           (Pressex 1x, Helddown)
    |     #     |        Minus: Next Weapon
    |           |
    |    (1)    |          One: Primary Weapon, Melee Weapon, Team Voice Chat    (Pressed 1x, Pressed 2x, Helddown)
    |           |    One & Two: Bandage Yourself (Hold)
    |    (2)    |          Two: Secondary Weapon, Gas Mask, NCO Voice Chat       (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |  o o o o  |          Joy: Move, Sprint
    |    Wii    |            C: Jump
    |___________|            Z: Crouch, Prone                                    (Pressed 1x. Helddown)

*/

//  Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

//  Box Size
var.BoxWidth = 20
var.BoxHeight = 15

//  Sensitivity Adjustments
var.Sensitivity = 7
var.AutoTurnSpeed = 30

//  Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 250ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    If Helddown(Wiimote.PointerVisible, 500ms)

       Mouse.DirectInputX += var.ModifyerX
       Mouse.DirectInputY += var.ModifyerY

    EndIf

Endif

//  Button Mapping
Key.5                =    Wiimote.Up
Key.R                =    Wiimote.Left
Key.F5               =    Wiimote.Right
Key.E                =    Keepdown(Wiimote.Down, 250ms) and not Wiimote.Down
Key.Q                =    Helddown(Wiimote.Down, 150ms)

Key.Shift            =    Wiimote.A and Helddown(abs(Wiimote.Nunchuk.JoyX) < 0.15, 75ms) and Helddown(abs(Wiimote.Nunchuk.JoyY) < 0.15, 100ms)
Mouse.RightButton    =    Keepdown(Wiimote.A, 150ms) and not Wiimote.A
Mouse.LeftButton     =    Wiimote.B

Mouse.WheelDown      =    Wiimote.Plus
Key.M                =    Helddown(Wiimote.Home, 150ms)
Key.N                =    Keepdown(Wiimote.Home, 250ms) and not Wiimote.Home
Mouse.WheelUp        =    Wiimote.Minus

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms)
Key.3                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms)
Key.LeftAlt          =    Helddown(Wiimote.One, 150ms) and Helddown(not Wiimote.Two, 150ms)

Key.B                =    Helddown(Wiimote.One and Wiimote.Two, 250ms)

Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms)
Key.4                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms)
Key.V                =    Helddown(Wiimote.Two, 150ms) and Helddown(not Wiimote.One, 150ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Shift            =    Wiimote.Nunchuk.JoyY < -0.65 and Helddown(not Wiimote.A, 500ms) and Helddown(not Wiimote.Home, 250ms)

Key.Space            =    Wiimote.Nunchuk.C
Key.Ctrl             =    Helddown(Wiimote.Nunchuk.ZButton, 250ms)
Key.C                =    Keepdown(Wiimote.Nunchuk.ZButton, 250ms) and not Wiimote.Nunchuk.ZButton
