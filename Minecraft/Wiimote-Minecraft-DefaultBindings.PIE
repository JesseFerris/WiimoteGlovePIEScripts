/*
    Wiimote-Minecraft-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Open and run this script with GlovePIE.exe by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)

    Description: Emulates Mouse and Keyboard from Wiimote input.

    Game Settings: Default Bindings.

     ___________
    | @   _     |       Up: Toggle Perspective
    |   _| |_   |     Left: Swap Item With Off-Hand
    |  |_   _|  |    Right: Advancements
    |    |_|    |     Down: Drop
    |           |
    |    (A)    |        A: Usde Item/Place Block
    |           |        B: Attack/Destroy
    |           |
    |  -  &  +  |     Plus: Mouse Wheel Down
    |           |     Home: Game Menu
    |     #     |    Minus: Mouse Wheel Up
    |           |
    |    (1)    |      One: Hotbar Slot 1, 3, 5             (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |    (2)    |      Two: Hotbar Slot 2, 4, 6             (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |  o o o o  |      Joy: Move, Sprint
    |    Wii    |        C: Jump
    |___________|        Z: Sneak

*/

//  Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

//  Box Size
var.BoxWidth = 20
var.BoxHeight = 15

//  Sensitivity Adjustments
var.Sensitivity = 15
var.AutoTurnSpeed = 30

//  Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 250ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    If Helddown(Wiimote.PointerVisible, 500ms)

        Mouse.DirectInputX += var.ModifyerX
        Mouse.DirectInputY += var.ModifyerY

    EndIf

Endif

//  Button Mapping
Key.F5               =    Wiimote.Up
Key.F                =    Wiimote.Left
Key.L                =    Wiimote.Right
Key.Q                =    Wiimote.Down

Mouse.RightButton    =    Wiimote.A
Mouse.LeftButton     =    Wiimote.B

Mouse.WheelDown      =    Wiimote.Plus
Key.E                =    Wiimote.Home
Mouse.WheelUp        =    Wiimote.Minus

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms)
Key.3                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms)
Key.5                =    Helddown(Wiimote.One, 150ms)

Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms)
Key.4                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms)
Key.6                =    Helddown(Wiimote.Two, 150ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Ctrl             =    Wiimote.Nunchuk.JoyY < -0.65

Key.Space            =    Wiimote.Nunchuk.CButton
Key.Shift            =    Wiimote.Nunchuk.ZButton
