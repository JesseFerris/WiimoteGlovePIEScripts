/*
    Wiimote-GroundBranch-DefaultBindings.PIE
    Copyright (C) 2024  Jesse Ferris

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

/*
    How To Use: Connect Wiimote via Bluetooth. Open and run this script with GlovePIE.exe by Carl Kenner. (http://sites.google.com/site/carlkenner/glovepie) (https://github.com/GlovePIEPreservation/GlovePIE?tab=readme-ov-file)

    Description: Emulates Mouse and Keyboard from Wiimote input.

    Game Settings: Default Bindings.

     ___________
    | @   _     |       Up: Jump, Drop Item, IR Illuminator                        (Pressed 1x or held in game, Pressed 2x, Pressed with Wiimote B)
    |   _| |_   |     Left: Lean left, Tac light                                   (Helddown, Pressed with Wiimote B)
    |  |_   _|  |    Right: Toggle Laser Mode, Lean Right, Laser                   (Pressed 1x, Helddown, Pressed with Wiimote B)
    |    |_|    |     Down: Crouch, Prone                                          (Pressed 1x, Helddown)
    |           |
    |    (A)    |        A: Aim Down Sights, Steady Aim                            (Pressed 1x, Helddown)
    |           |        B: Interact                                               (Pressed with Wiimote A)
    |           |
    |  -  &  +  |     Plus: Raise Weapon / Switch Sights (ADS)
    |           |     Home: Tablet, Coms Wheel, Attachments Wheel, Night Vision    (Pressed 1x, Pressed 2x, Helddown, Pressed with Wiimote B)
    |     #     |    Minus: Lower Weapon / Switch Sights (ADS)
    |           |
    |    (1)    |      One: Primary Weapon, Grenades, Proximity VOIP               (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |    (2)    |      Two: Secondary Weapon, Equipment, Radio VOIP                (Pressed 1x, Pressed 2x, Helddown)
    |           |
    |  o o o o  |      Joy: Move, Run, Sprint
    |    Wii    |        C: Reload/Quickreload/Inspect Ammo, Fire Mode Selector    (Pressed 1x/2x/held in game, Pressed with Wiimote B)
    |___________|        Z: Fire/Throw

*/

//  Set GlovePIE Frame Rate to Wiimote IR Camera Hz, Disable LEDs
Pie.RealFrameRate = 100Hz
Wiimote.Leds = 0

//  Box Size
var.BoxWidth = 20
var.BoxHeight = 15

//  Sensitivity Adjustments
var.Sensitivity = 15
var.AutoTurnSpeed = 30

//  Pointer Tracking, Auto-turn, Mouse Output
If Helddown(Wiimote.PointerVisible, 250ms)

    var.PointerX = (Wiimote.PointerX - 1/2) * 100
    var.PointerY = (Wiimote.PointerY - 1/2) * 100

    var.ModifyerX = Delta(var.PointerX) * var.Sensitivity
    var.ModifyerY = Delta(var.PointerY) * var.Sensitivity
                                                                                    
    If abs(var.PointerX) > var.BoxWidth then var.ModifyerX += ((abs(var.PointerX) - var.BoxWidth) / var.PointerX) * var.AutoTurnSpeed
    If abs(var.PointerY) > var.BoxHeight then var.ModifyerY += ((abs(var.PointerY) - var.BoxHeight) / var.PointerY) * var.AutoTurnSpeed

    If Helddown(Wiimote.PointerVisible, 500ms)

        Mouse.DirectInputX += var.ModifyerX
        Mouse.DirectInputY += var.ModifyerY

    EndIf

Endif

//  Button Mapping
Key.8                =    Wiimote.Up and Wiimote.B
Key.Space            =    Keepdown(Wiimote.Up, 250ms) and Helddown(not Wiimote.Up, 100ms) and not Wiimote.B
Key.J                =    Keepdown(not Wiimote.Up, 250ms) and Helddown(Wiimote.Up, 25ms) and not Wiimote.B
Key.5                =    Wiimote.Left and Wiimote.B
Key.Q                =    Helddown(Wiimote.Left, 150ms) and not Wiimote.B
Key.6                =    Wiimote.Right and Wiimote.B
Key.7                =    Keepdown(Wiimote.Right, 250ms) and Helddown(not Wiimote.Right, 100ms) and not Wiimote.B
Key.E                =    Helddown(Wiimote.Right, 150ms) and not Wiimote.B
Key.C                =    Keepdown(Wiimote.Down, 250ms) and Helddown(not Wiimote.Down, 100ms)
Key.Z                =    Helddown(Wiimote.Down, 150ms)

Key.Ctrl             =    (Wiimote.A and Helddown(abs(Wiimote.Nunchuk.JoyX) < 0.15, 350ms) and Helddown(abs(Wiimote.Nunchuk.JoyY) < 0.15, 350ms)) and Helddown(not Wiimote.B, 250ms)
Mouse.RightButton    =    (Keepdown(Wiimote.A, 150ms) but not Wiimote.A)  and Helddown(not Wiimote.B, 250ms)
Key.F                =    Wiimote.A and Wiimote.B

Key.Home             =    Helddown(Wiimote.Plus, 150ms) and Wiimote.B
Mouse.WheelUp        =    Keepdown(Wiimote.Plus, 150ms) and not Wiimote.Plus
Key.M                =    Keepdown(Wiimote.Home, 250ms) and Helddown(not Wiimote.Home, 100ms) and not Wiimote.B
Key.N                =    Keepdown(Wiimote.Home, 250ms) and Helddown(not Wiimote.Home, 100ms) and Wiimote.B
Key.G                =    Keepdown(not Wiimote.Home, 250ms) and Helddown(Wiimote.Home, 25ms)
Mouse.MiddleButton   =    Helddown(Wiimote.Home, 150ms) and not Wiimote.B
Mouse.WheelDown      =    Keepdown(Wiimote.Minus, 150ms) and not Wiimote.Minus
Key.End              =    Helddown(Wiimote.Minus, 150ms) and Wiimote.B

Key.1                =    Keepdown(Wiimote.One, 250ms) and Helddown(not Wiimote.One, 100ms)
Key.3                =    Keepdown(not Wiimote.One, 250ms) and Helddown(Wiimote.One, 25ms)
Key.V                =    Helddown(Wiimote.One, 150ms)

Key.2                =    Keepdown(Wiimote.Two, 250ms) and Helddown(not Wiimote.Two, 100ms)
Key.4                =    Keepdown(not Wiimote.Two, 250ms) and Helddown(Wiimote.Two, 25ms)
Key.B                =    Helddown(Wiimote.Two, 150ms)

Key.W                =    Wiimote.Nunchuk.JoyY < -0.15
Key.S                =    Wiimote.Nunchuk.JoyY > 0.15
Key.A                =    Wiimote.Nunchuk.JoyX < -0.15
Key.D                =    Wiimote.Nunchuk.JoyX > 0.15
Key.Ctrl             =    Wiimote.Nunchuk.JoyY < -0.65
Key.Shift            =    ((abs(Wiimote.Nunchuk.JoyX) > 0.35) or (abs(Wiimote.Nunchuk.JoyY) > 0.35)) but not (Wiimote.Nunchuk.JoyY < -0.65)

Key.Alt              =    Helddown(Wiimote.B, 150ms)
Key.R                =    Wiimote.Nunchuk.CButton and Helddown(not Wiimote.B, 250ms)
Key.X                =    Wiimote.Nunchuk.CButton and Wiimote.B
Mouse.LeftButton     =    Wiimote.Nunchuk.ZButton
